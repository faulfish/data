; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=QSyncerV2
AppVerName=QSyncer 2.0
AppPublisher=Qisda Tool Team
DefaultDirName={pf}\Mobile Utility\QSyncerV2
DefaultGroupName=Mobile Utility\QSyncerV2
AllowNoIcons=true
OutputDir=C:\QSyncher_V2\Code\Apps
OutputBaseFilename=QSyncerV2Setup
;SetupIconFile=D:\QSyncherDailyBuild\favicon.ico
Compression=lzma/ultra
SolidCompression=true
InternalCompressLevel=ultra
VersionInfoVersion=2.0.0.0
VersionInfoCompany=Qisda
VersionInfoDescription=QSyncer V2.0
VersionInfoCopyright=Qisda
AppCopyright=Qisda
AppID={{19CBEDA6-F36B-43D6-8F46-42A8766939EE}
;WizardImageFile=D:\QSyncherDailyBuild\QisdaTitle.bmp
;WizardSmallImageFile=D:\QSyncherDailyBuild\Small Q.bmp
PrivilegesRequired=admin

[Languages]
Name: english; MessagesFile: compiler:Default.isl
Name: chinesesimp; MessagesFile: compiler:Languages\ChineseSimp.isl
Name: chinesetrad; MessagesFile: compiler:Languages\ChineseTrad.isl
Name: italian; MessagesFile: compiler:Languages\Italian.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; Localization, Configuration, Pictures
Source: ..\debug\Localization\*; DestDir: {app}\Localization\; Flags: recursesubdirs ignoreversion createallsubdirs; Tasks: ; Languages:
Source: ..\debug\Configuration\*; DestDir: {app}\Configuration\; Flags: recursesubdirs ignoreversion createallsubdirs
Source: ..\debug\Pictures2\*; DestDir: {app}\Pictures2\; Flags: recursesubdirs ignoreversion createallsubdirs
Source: ..\debug\DefaultSetting\*.*; DestDir: {commondocs}\QSyncer\; Flags: recursesubdirs ignoreversion createallsubdirs

; MFC related files
Source: .\Microsoft.VC80.CRT\*; DestDir: {app}\Microsoft.VC80.CRT\; Flags: recursesubdirs ignoreversion createallsubdirs
Source: .\Microsoft.VC80.MFC\*; DestDir: {app}\Microsoft.VC80.MFC\; Flags: recursesubdirs ignoreversion createallsubdirs
Source: .\Microsoft.VC80.MFCLOC\*; DestDir: {app}\Microsoft.VC80.MFCLOC\; Flags: recursesubdirs ignoreversion createallsubdirs
Source: .\Microsoft.VC80.MFC\mfc80u.dll; DestDir: {app}; Flags: ignoreversion 32bit
Source: .\Microsoft.VC80.CRT\msvcp80.dll; DestDir: {app}; Flags: ignoreversion 32bit
Source: .\Microsoft.VC80.CRT\msvcr80.dll; DestDir: {app}; Flags: ignoreversion 32bit
Source: ..\..\ShareLib\GdiPlus\bin\gdiplus.dll; DestDir: {app}; Flags: ignoreversion 32bit

; Builded Qsyncer 2.0 files
Source: .\*.exe; DestDir: {app}; Flags: recursesubdirs ignoreversion createallsubdirs
Source: .\*.dll; DestDir: {app}; Flags: recursesubdirs ignoreversion createallsubdirs
Source: .\BenQWCDMAINSTALL.msi; DestDir: {app}; Flags: ignoreversion; AfterInstall: InstallDriver()

; Icon files (for Registries used)
Source: ..\debug\extension\QMSG.ico; DestDir: {app}\extension\; Flags: ignoreversion
Source: ..\debug\extension\VCF.ico; DestDir: {app}\extension\; Flags: ignoreversion
Source: ..\debug\extension\VCS.ico; DestDir: {app}\extension\; Flags: ignoreversion

; Help files
Source: ..\debug\help\QSyncer_help_cht.pdf; DestDir: {app}\help\; Flags: ignoreversion
Source: ..\debug\help\QSyncer_help_enu.pdf; DestDir: {app}\help\; Flags: ignoreversion

[Registry]
Root: HKCR; Subkey: .vcs; ValueType: string; ValueData: vcsfile; Flags: createvalueifdoesntexist
Root: HKCR; Subkey: .vcf; ValueType: string; ValueData: vcffile; Flags: createvalueifdoesntexist
Root: HKCR; Subkey: .qmsg; ValueType: string; ValueData: qmsgfile; Flags: createvalueifdoesntexist
Root: HKCR; Subkey: vcsfile\DefaultIcon; ValueType: string; ValueData: {app}\extension\vcs.ico; Flags: createvalueifdoesntexist
Root: HKCR; Subkey: vcffile\DefaultIcon; ValueType: string; ValueData: {app}\extension\vcf.ico; Flags: createvalueifdoesntexist
Root: HKCR; Subkey: qmsgfile\DefaultIcon; ValueType: string; ValueData: {app}\extension\qmsg.ico; Flags: createvalueifdoesntexist

[Icons]
Name: {group}\QSyncerV2; Filename: {app}\QSyncer2.exe; WorkingDir: {app}
Name: {group}\{cm:UninstallProgram,QSyncerV2}; Filename: {uninstallexe}
Name: {commondesktop}\QSyncerV2; Filename: {app}\QSyncer2.exe; Tasks: desktopicon; WorkingDir: {app}
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\QSyncerV2; Filename: {app}\QSyncer2.exe; Tasks: quicklaunchicon; WorkingDir: {app}

[Run]
Filename: {app}\QSyncer2.exe; Description: {cm:LaunchProgram,QSyncerV2}; Flags: nowait postinstall skipifsilent

[Code]
procedure InstallDriver();
var ErrorCode: Integer;
var Version: TWindowsVersion;
var S: String;
begin
  // Get Windows version
  GetWindowsVersionEx(Version);
  // if the windows version is windows 2000 sp4
  if Version.NTPlatform and (Version.Major = 5) and (Version.Minor = 0) then begin
    if not ShellExec('', 'msiexec', '/i "' + ExpandConstant('{app}\BenQWCDMAINSTALL.msi') + '"', '', SW_SHOW, ewWaitUntilTerminated, ErrorCode) then begin
      MsgBox('Install Driver Failed!!!', mbInformation, MB_OK);
    end;
  end else begin
    if not ShellExec('', 'msiexec', '/i "' + ExpandConstant('{app}\BenQWCDMAINSTALL.msi') + '"' + ' /passive /quiet', '', SW_SHOW, ewWaitUntilTerminated, ErrorCode) then begin
      MsgBox('Install Driver Failed!!!', mbInformation, MB_OK);
    end;
  end;
end;

function InitializeUninstall(): Boolean;
var retCode: Boolean;
var ErrorCode: Integer;
var Version: TWindowsVersion;
begin
  GetWindowsVersionEx(Version);
  retCode := true;
  if Version.NTPlatform and (Version.Major = 5) and (Version.Minor = 0) then begin
    if not ShellExec('', 'msiexec', '/x "' + ExpandConstant('{app}\BenQWCDMAINSTALL.msi') + '"', '', SW_SHOW, ewWaitUntilTerminated, ErrorCode) then begin
      MsgBox('Uninstall Driver Failed!!!', mbInformation, MB_OK);
      retCode := False;
    end;
  end else begin
    if not ShellExec('', 'msiexec', '/x "' + ExpandConstant('{app}\BenQWCDMAINSTALL.msi') + '"' + ' /passive /quiet', '', SW_SHOW, ewWaitUntilTerminated, ErrorCode) then begin
      MsgBox('Uninstall Driver Failed!!!', mbInformation, MB_OK);
      retCode := False;
    end;
  end;
  result := retCode;
end;

function NeedRestart(): Boolean;
var Version: TWindowsVersion;
var S: String;
var ErrorCode: Integer;
begin
  GetWindowsVersionEx(Version);
  // that means NT platform, Windows 2000, Service Pack 4
  if Version.NTPlatform and (Version.Major = 5) and (Version.Minor = 0) and (Version.ServicePackMajor >= 4)then begin
    result := true;
  end else begin
    result := false;
  end;
end;
